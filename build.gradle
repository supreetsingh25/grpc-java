apply plugin: 'java'
apply plugin: 'com.google.protobuf'

buildscript {
  repositories {
    maven {
      // The google mirror is less flaky than mavenCentral()
      url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
  }
  dependencies {
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
    // gradle versions
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
  }
}

repositories {
  maven {
      // The google mirror is less flaky than mavenCentral()
    url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
  }
  mavenLocal()
}

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.12.0' // CURRENT_GRPC_VERSION
def nettyTcNativeVersion = '2.0.7.Final'

dependencies {
  compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
  compile "io.grpc:grpc-alts:${grpcVersion}"
  compile "io.grpc:grpc-netty:${grpcVersion}"
  compile "io.grpc:grpc-protobuf:${grpcVersion}"
  compile "io.grpc:grpc-stub:${grpcVersion}"
  compileOnly "javax.annotation:javax.annotation-api:1.2"

}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.5.1-1'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

// Generate IntelliJ IDEA's .idea & .iml project files
apply plugin: 'eclipse'

// Provide convenience executables for trying out the examples.
apply plugin: 'application'

startScripts.enabled = false


task serverCommunication(type: CreateStartScripts) {
  mainClassName = 'io.grpc.communication.example.ServerTerminal'
  applicationName = 'server-terminal'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}

task clientCommunication(type: CreateStartScripts) {
  mainClassName = 'io.grpc.communication.example.ClientTerminal'
  applicationName = 'client-terminal'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtime
}


applicationDistribution.into('bin') {
  from(serverCommunication)
  from(clientCommunication)
  fileMode = 0755
}
